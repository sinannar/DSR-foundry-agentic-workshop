metadata name = 'Cognitive Services Project'
metadata description = '''
This module deploys a Project within a Cognitive Services account.
It allows for the creation of a Foundry Project with optional managed identities and role assignments.
'''

@sys.description('Required. The name of the parent Cognitive Services account.')
param accountName string

@sys.description('Required. The name of the Foundry Project.')
param name string

@sys.description('Required. The location for the Foundry Project.')
param location string

import { diagnosticSettingFullType } from 'br/public:avm/utl/types/avm-common-types:0.6.1'
@sys.description('Optional. The diagnostic settings of the service.')
param diagnosticSettings diagnosticSettingFullType[]?


@sys.description('Optional. Resource tags for the Foundry Project.')
param tags object?

import { managedIdentityAllType } from 'br/public:avm/utl/types/avm-common-types:0.6.1'
@sys.description('Optional. The managed identity definition for this resource.')
param managedIdentities managedIdentityAllType?

import { roleAssignmentType } from 'br/public:avm/utl/types/avm-common-types:0.6.0'
@sys.description('Optional. Role assignments to apply to the Foundry Project.')
param roleAssignments roleAssignmentType[]?

@sys.description('Required. Display name of the Foundry Project.')
param displayName string

@sys.description('Optional. Description of the Foundry Project.')
param description string = ''

import { connectionType } from '../connection/main.bicep'
@sys.description('Optional. Connections to create in the Foundry Project.')
param connections connectionType[] = []

import { applicationType } from './application/main.bicep'
@sys.description('Optional. Applications to create in the Foundry Project.')
param applications applicationType[] = []

import { projectCapabilityHostType } from './capabilityHost/main.bicep'
@sys.description('Optional. Capability hosts to create in the Foundry Project. These configure per-project storage backends for threads, vectors, and files.')
param capabilityHosts projectCapabilityHostType[] = []


var formattedUserAssignedIdentities = reduce(
  map((managedIdentities.?userAssignedResourceIds ?? []), (id) => { '${id}': {} }),
  {},
  (cur, next) => union(cur, next)
) // Converts the flat array to an object like { '${id1}': {}, '${id2}': {} }
var identityType = !empty(managedIdentities)
  ? (managedIdentities.?systemAssigned ?? false)
    ? (!empty(managedIdentities.?userAssignedResourceIds ?? {}) ? 'SystemAssigned, UserAssigned' : 'SystemAssigned')
    : (!empty(managedIdentities.?userAssignedResourceIds ?? {}) ? 'UserAssigned' : 'None')
  : 'None'

var identity = !empty(managedIdentities)
  ? union(
      {
        type: identityType
      },
      !empty(formattedUserAssignedIdentities) ? { userAssignedIdentities: formattedUserAssignedIdentities } : {}
    )
  : null

var builtInRoleNames = {
  'Azure AI Account Owner': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'e47c6f54-e4a2-4754-9501-8e0985b135e1'
  )
  'Azure AI Administrator': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'b78c5d69-af96-48a3-bf8d-a8b4d589de94'
  )
  'Azure AI Developer': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '64702f94-c441-49e6-a78b-ef80e0188fee'
  )
  'Azure AI Enterprise Network Connection Approver': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'b556d68e-0be0-4f35-a333-ad7ee1ce17ea'
  )
  'Azure AI Project Manager': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'eadc314b-1a2d-4efa-be10-5d325db5065e'
  )
  'Azure AI User': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '53ca6127-db72-4b80-b1b0-d745d6d5456d'
  )
  'Cognitive Services Contributor': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '25fbc0a9-bd7c-42a3-aa1a-3b75d497ee68'
  )
  'Cognitive Services Custom Vision Contributor': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'c1ff6cc2-c111-46fe-8896-e0ef812ad9f3'
  )
  'Cognitive Services Custom Vision Deployment': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '5c4089e1-6d96-4d2f-b296-c1bc7137275f'
  )
  'Cognitive Services Custom Vision Labeler': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '88424f51-ebe7-446f-bc41-7fa16989e96c'
  )
  'Cognitive Services Custom Vision Reader': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '93586559-c37d-4a6b-ba08-b9f0940c2d73'
  )
  'Cognitive Services Custom Vision Trainer': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '0a5ae4ab-0d65-4eeb-be61-29fc9b54394b'
  )
  'Cognitive Services Data Reader (Preview)': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'b59867f0-fa02-499b-be73-45a86b5b3e1c'
  )
  'Cognitive Services Face Recognizer': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '9894cab4-e18a-44aa-828b-cb588cd6f2d7'
  )
  'Cognitive Services Immersive Reader User': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'b2de6794-95db-4659-8781-7e080d3f2b9d'
  )
  'Cognitive Services Language Owner': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'f07febfe-79bc-46b1-8b37-790e26e6e498'
  )
  'Cognitive Services Language Reader': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '7628b7b8-a8b2-4cdc-b46f-e9b35248918e'
  )
  'Cognitive Services Language Writer': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'f2310ca1-dc64-4889-bb49-c8e0fa3d47a8'
  )
  'Cognitive Services LUIS Owner': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'f72c8140-2111-481c-87ff-72b910f6e3f8'
  )
  'Cognitive Services LUIS Reader': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '18e81cdc-4e98-4e29-a639-e7d10c5a6226'
  )
  'Cognitive Services LUIS Writer': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '6322a993-d5c9-4bed-b113-e49bbea25b27'
  )
  'Cognitive Services Metrics Advisor Administrator': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'cb43c632-a144-4ec5-977c-e80c4affc34a'
  )
  'Cognitive Services Metrics Advisor User': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '3b20f47b-3825-43cb-8114-4bd2201156a8'
  )
  'Cognitive Services OpenAI Contributor': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'a001fd3d-188f-4b5d-821b-7da978bf7442'
  )
  'Cognitive Services OpenAI User': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '5e0bd9bd-7b93-4f28-af87-19fc36ad61bd'
  )
  'Cognitive Services QnA Maker Editor': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'f4cc2bf9-21be-47a1-bdf1-5c5804381025'
  )
  'Cognitive Services QnA Maker Reader': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '466ccd10-b268-4a11-b098-b4849f024126'
  )
  'Cognitive Services Speech Contributor': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '0e75ca1e-0464-4b4d-8b93-68208a576181'
  )
  'Cognitive Services Speech User': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'f2dc8367-1007-4938-bd23-fe263f013447'
  )
  'Cognitive Services User': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'a97b65f3-24c7-4388-baec-2e87135dc908'
  )
  Contributor: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', 'b24988ac-6180-42a0-ab88-20f7382dd24c')
  Owner: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '8e3af657-a8ff-443c-a75c-2fe8c4bcb635')
  Reader: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', 'acdd72a7-3385-48ef-bd42-f606fba81ae7')
  'Role Based Access Control Administrator': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    'f58310d9-a9f6-439a-9e8d-f62e7b41a168'
  )
  'User Access Administrator': subscriptionResourceId(
    'Microsoft.Authorization/roleDefinitions',
    '18d7d88d-d35e-4fb5-a5c3-7773c20a72d9'
  )
}

var formattedRoleAssignments = [
  for (roleAssignment, index) in (roleAssignments ?? []): union(roleAssignment, {
    roleDefinitionId: builtInRoleNames[?roleAssignment.roleDefinitionIdOrName] ?? (contains(
        roleAssignment.roleDefinitionIdOrName,
        '/providers/Microsoft.Authorization/roleDefinitions/'
      )
      ? roleAssignment.roleDefinitionIdOrName
      : subscriptionResourceId('Microsoft.Authorization/roleDefinitions', roleAssignment.roleDefinitionIdOrName))
  })
]

resource parentAccount 'Microsoft.CognitiveServices/accounts@2025-10-01-preview' existing = {
  name: accountName
}

@onlyIfNotExists()
resource project 'Microsoft.CognitiveServices/accounts/projects@2025-10-01-preview' = {
  parent: parentAccount
  name: name
  location: location
  tags: tags ?? {}
  identity: identity ?? { type: 'None' }
  properties: {
    displayName: displayName
    description: description
  }
}

resource project_connections 'Microsoft.CognitiveServices/accounts/projects/connections@2025-10-01-preview' = [
  for (connection, index) in (connections ?? []): {
    parent: project
    name: connection.name
    properties: union(connection.connectionProperties, {
      category: connection.category
      expiryTime: connection.?expiryTime
      isSharedToAll: connection.?isSharedToAll
      metadata: connection.?metadata
      sharedUserList: connection.?sharedUserList
      target: connection.target
    })
  }
]

// Helper function to build connection resource ID from connection name
func buildConnectionResourceId(accountId string, connectionName string) string =>
  '${accountId}/connections/${connectionName}'

@batchSize(1)
module project_capabilityHosts './capabilityHost/main.bicep' = [
  for (capabilityHost, index) in (capabilityHosts ?? []): {
    name: '${take('${accountName}-${name}-${capabilityHost.name}', 60)}-cph'
    dependsOn: [
      project_connections
    ]
    params: {
      accountName: accountName
      projectName: project.name
      name: capabilityHost.name
      aiServicesConnections: capabilityHost.?aiServicesConnectionNames != null
        ? map(capabilityHost.aiServicesConnectionNames!, connName => buildConnectionResourceId(parentAccount.id, connName))
        : null
      threadStorageConnections: capabilityHost.?threadStorageConnectionNames != null
        ? map(capabilityHost.threadStorageConnectionNames!, connName => buildConnectionResourceId(parentAccount.id, connName))
        : null
      vectorStoreConnections: capabilityHost.?vectorStoreConnectionNames != null
        ? map(capabilityHost.vectorStoreConnectionNames!, connName => buildConnectionResourceId(parentAccount.id, connName))
        : null
      storageConnections: capabilityHost.?storageConnectionNames != null
        ? map(capabilityHost.storageConnectionNames!, connName => buildConnectionResourceId(parentAccount.id, connName))
        : null
    }
  }
]

// Filter applications to only those with agents defined (RP requires agents to be non-empty)
var deployableApplications = filter(applications ?? [], app => !empty(app.?agents ?? []))

@batchSize(1)
module project_applications './application/main.bicep' = [
  for (application, index) in deployableApplications: {
    name: '${take('${accountName}-${name}-${application.name}', 60)}-app'
    params: {
      accountName: accountName
      projectName: project.name
      name: application.name
      displayName: application.?displayName
      description: application.?description
      authorizationPolicy: application.?authorizationPolicy
      agentIdentityBlueprint: application.?agentIdentityBlueprint
      defaultInstanceIdentity: application.?defaultInstanceIdentity
      baseUrl: application.?baseUrl
      trafficRoutingPolicy: application.?trafficRoutingPolicy
      agents: application.agents
      tags: application.?tags
      agentDeployments: application.?agentDeployments ?? []
    }
  }
]

resource project_roleAssignments 'Microsoft.Authorization/roleAssignments@2022-04-01' = [
  for (roleAssignment, index) in (formattedRoleAssignments ?? []): {
    name: roleAssignment.?name ?? guid(project.id, roleAssignment.principalId, roleAssignment.roleDefinitionId)
    properties: {
      roleDefinitionId: roleAssignment.roleDefinitionId
      principalId: roleAssignment.principalId
      description: roleAssignment.?description
      principalType: roleAssignment.?principalType
      condition: roleAssignment.?condition
      conditionVersion: !empty(roleAssignment.?condition) ? (roleAssignment.?conditionVersion ?? '2.0') : null
      delegatedManagedIdentityResourceId: roleAssignment.?delegatedManagedIdentityResourceId
    }
    scope: project
  }
]

resource project_diagnosticSettings 'Microsoft.Insights/diagnosticSettings@2021-05-01-preview' = [
  for (diagnosticSetting, index) in (diagnosticSettings ?? []): {
    name: diagnosticSetting.?name ?? '${name}-diagnosticSettings'
    properties: {
      storageAccountId: diagnosticSetting.?storageAccountResourceId
      workspaceId: diagnosticSetting.?workspaceResourceId
      eventHubAuthorizationRuleId: diagnosticSetting.?eventHubAuthorizationRuleResourceId
      eventHubName: diagnosticSetting.?eventHubName
      metrics: [
        for group in (diagnosticSetting.?metricCategories ?? [{ category: 'AllMetrics' }]): {
          category: group.category
          enabled: group.?enabled ?? true
          timeGrain: null
        }
      ]
      logs: [
        for group in (diagnosticSetting.?logCategoriesAndGroups ?? [{ categoryGroup: 'allLogs' }]): {
          categoryGroup: group.?categoryGroup
          category: group.?category
          enabled: group.?enabled ?? true
        }
      ]
      marketplacePartnerId: diagnosticSetting.?marketplacePartnerResourceId
      logAnalyticsDestinationType: diagnosticSetting.?logAnalyticsDestinationType
    }
    scope: project
  }
]

@sys.description('The resource ID of the created Foundry Project.')
output projectResourceId string = project.id

@sys.description('The name of the created Foundry Project.')
output projectResourceName string = project.name

@sys.description('The principal ID of the system assigned identity.')
output systemAssignedMIPrincipalId string? = project.?identity.?principalId

@sys.description('The name of the resource group the Foundry Project created in.')
output resourceGroupName string = resourceGroup().name

import { applicationOutputType } from './application/main.bicep'
@sys.description('The applications created in the Foundry Project.')
output applications applicationOutputType[] = [
  for (application, index) in deployableApplications: {
    name: project_applications[index].outputs.name
    resourceId: project_applications[index].outputs.resourceId
  }
]

import { projectCapabilityHostOutputType } from './capabilityHost/main.bicep'
@sys.description('The capability hosts created in the Foundry Project.')
output capabilityHostsOutput projectCapabilityHostOutputType[] = [
  for (capabilityHost, index) in (capabilityHosts ?? []): {
    name: project_capabilityHosts[index].outputs.name
    resourceId: project_capabilityHosts[index].outputs.resourceId
  }
]
